cmake_minimum_required(VERSION 3.12)
project(ninja)

if(CMAKE_BUILD_TYPE MATCHES "Release")
	cmake_policy(SET CMP0069 NEW)
	include(CheckIPOSupported)
	check_ipo_supported(RESULT lto_supported OUTPUT error)

	if(lto_supported)
		message(STATUS "IPO / LTO enabled")
		set(CMAKE_INTERPROCEDURAL_OPTIMIZATION TRUE)
	else()
		message(STATUS "IPO / LTO not supported: <${error}>")
	endif()
endif()

option(SEPARATE_LIBNINJA OFF)

if(MSVC)
	set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /W4 /GR- /Zc:__cplusplus")
else()
	set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wno-deprecated -fdiagnostics-color")
endif()

find_program(RE2C re2c)
if(RE2C)
	# the depfile parser and ninja lexers are generated using re2c.
	function(re2c IN OUT)
		add_custom_command(DEPENDS ${IN} OUTPUT ${OUT}
			COMMAND ${RE2C} -b -i --no-generation-date -o ${OUT} ${IN}
		)
	endfunction()
	re2c(${CMAKE_SOURCE_DIR}/src/depfile_parser.in.cc ${CMAKE_BINARY_DIR}/depfile_parser.cc)
	re2c(${CMAKE_SOURCE_DIR}/src/lexer.in.cc ${CMAKE_BINARY_DIR}/lexer.cc)
	add_library(libninja-re2c OBJECT ${CMAKE_BINARY_DIR}/depfile_parser.cc ${CMAKE_BINARY_DIR}/lexer.cc)
else()
	message(WARNING "re2c was not found; changes to src/*.in.cc will not affect your build.")
	add_library(libninja-re2c OBJECT src/depfile_parser.cc src/lexer.cc)
endif()
target_include_directories(libninja-re2c PRIVATE src)
set_target_properties(libninja-re2c PROPERTIES PREFIX "")

if(SEPARATE_LIBNINJA)
	set(LIBNINJA_MODULE_TYPE "SHARED")
else()
	set(LIBNINJA_MODULE_TYPE "STATIC")
endif()

# Core source files all build into ninja library.
add_library(libninja "${LIBNINJA_MODULE_TYPE}"
	src/build_log.cc
	src/build.cc
	src/clean.cc
	src/clparser.cc
	src/dyndep.cc
	src/dyndep_parser.cc
	src/debug_flags.cc
	src/deps_log.cc
	src/disk_interface.cc
	src/edit_distance.cc
	src/eval_env.cc
	src/graph.cc
	src/graphviz.cc
	src/line_printer.cc
	src/manifest_parser.cc
	src/metrics.cc
	src/parser.cc
	src/state.cc
	src/string_piece_util.cc
	src/util.cc
	src/version.cc
)
target_link_libraries(libninja libninja-re2c)
set_target_properties(libninja PROPERTIES PREFIX "")

if(WIN32)
	target_sources(libninja PRIVATE
		src/subprocess-win32.cc
		src/includes_normalize-win32.cc
		src/msvc_helper-win32.cc
		src/msvc_helper_main-win32.cc
		src/getopt.c
	)
	if(MSVC)
		target_sources(libninja PRIVATE src/minidump-win32.cc)
	endif()
else()
	target_sources(libninja PRIVATE src/subprocess-posix.cc)
endif()

#Fixes GetActiveProcessorCount on MinGW
if(MINGW)
target_compile_definitions(libninja PRIVATE _WIN32_WINNT=0x0601 __USE_MINGW_ANSI_STDIO=1)
endif()

# Main executable is library plus main() function.
add_executable(ninja src/ninja.cc)
target_link_libraries(ninja PRIVATE libninja)

# Tests all build into ninja_test executable.
add_executable(ninja_test
	src/build_log_test.cc
	src/build_test.cc
	src/clean_test.cc
	src/clparser_test.cc
	src/depfile_parser_test.cc
	src/deps_log_test.cc
	src/disk_interface_test.cc
	src/dyndep_parser_test.cc
	src/edit_distance_test.cc
	src/graph_test.cc
	src/lexer_test.cc
	src/manifest_parser_test.cc
	src/ninja_test.cc
	src/state_test.cc
	src/string_piece_util_test.cc
	src/subprocess_test.cc
	src/test.cc
	src/util_test.cc
)
if(WIN32)
	target_sources(ninja_test PRIVATE src/includes_normalize_test.cc src/msvc_helper_test.cc)
endif()
target_link_libraries(ninja_test PRIVATE libninja)

foreach(perftest
  build_log_perftest
  canon_perftest
  clparser_perftest
  depfile_parser_perftest
  hash_collision_bench
  manifest_parser_perftest
)
  add_executable(${perftest} src/${perftest}.cc)
  target_link_libraries(${perftest} PRIVATE libninja libninja-re2c)
endforeach()

enable_testing()
add_test(NinjaTest ninja_test)

set(CPACK_PACKAGE_NAME "ninja-build")
set(CPACK_PACKAGE_VERSION_MAJOR "1")
set(CPACK_PACKAGE_VERSION_MINOR "9")
set(CPACK_PACKAGE_VERSION_PATCH "0")

set(CPACK_PACKAGE_VENDOR "https://github.com/ninja-build/ninja contributors")
set(CPACK_PACKAGE_DESCRIPTION_SUMMARY "Ninja is a small build system with a focus on speed. It differs from other build systems in two major respects: it is designed to have its input files generated by a higher-level build system, and it is designed to run builds as fast as possible.")
set(CPACK_PACKAGE_HOMEPAGE_URL "https://ninja-build.org/")
set(CPACK_PACKAGE_CONTACT "https://github.com/ninja-build/ninja")
set(CPACK_PACKAGE_CHECKSUM "SHA512")
set(CPACK_RESOURCE_FILE_LICENSE "${CMAKE_SOURCE_DIR}/COPYING")
set(CPACK_GENERATOR "DEB")
set(CPACK_PACKAGE_DESCRIPTION_FILE "${CMAKE_SOURCE_DIR}/doc/CPack.ninja-build.description.txt") # from https://salsa.debian.org/debian/ninja-build/raw/master/debian/control, copyright by Felix Geyer <fgeyer@debian.org> (https://salsa.debian.org/fgeyer)


include(CPackComponent)
set("CPACK_DEBIAN_COMPRESSION_TYPE" "xz")
set("CPACK_COMPONENTS_GROUPING" "IGNORE")

cpack_add_component("bin")
cpack_add_component("test")

cpack_add_component_group("misc"
	EXPANDED
)

cpack_add_component_group("completions"
	DISPLAY_NAME "Shell completions"
	DESCRIPTION "Provides completions for different shells"
	PARENT_GROUP "misc"
	EXPANDED
)
cpack_add_component_group("syntax"
	DISPLAY_NAME "Syntax highlighting"
	DESCRIPTION "Provides syntax highlighting for different editors"
	PARENT_GROUP "misc"
	EXPANDED
)

cpack_add_component("zsh"
	DISPLAY_NAME "zsh completions"
	GROUP "completions"
)
cpack_add_component("bash"
	DISPLAY_NAME "bash completions"
	GROUP "completions"
)
cpack_add_component("vim"
	DISPLAY_NAME "vim syntax"
	GROUP "syntax"
)
cpack_add_component("emacs"
	DISPLAY_NAME "emacs syntax"
	GROUP "syntax"
)


if(SEPARATE_LIBNINJA)
	cpack_add_component("lib")
	install(TARGETS libninja libninja-re2c ninja
		LIBRARY
		COMPONENT "lib"
	)
	set("CPACK_DEBIAN_LIB_PACKAGE_NAME" "libninjabuild")
	set("CPACK_COMPONENT_LIB_DISPLAY_NAME" "libninja")
	set("CPACK_COMPONENT_LIB_REQUIRED" TRUE)
	set(DEPENDS_ON_LIBNINJA ", ${CPACK_DEBIAN_LIB_PACKAGE_NAME}")

	cpack_add_component("dev"
		DEPENDS "lib"
	)
	install(TARGETS libninja libninja-re2c
		PUBLIC_HEADER ARCHIVE
		COMPONENT "dev"
	)
	set(CPACK_COMPONENT_BIN_DEPENDS "lib")
	set(CPACK_COMPONENT_TEST_DEPENDS "lib")
else()
	set(DEPENDS_ON_LIBNINJA "")
endif()


install(TARGETS ninja
	RUNTIME
	COMPONENT "bin"
)

# install rules from https://salsa.debian.org/debian/ninja-build/raw/master/debian/ninja-build.install, copyright by Felix Geyer <fgeyer@debian.org> (https://salsa.debian.org/fgeyer)
install(PROGRAMS "${CMAKE_SOURCE_DIR}/misc/bash-completion"
	DESTINATION "${CMAKE_INSTALL_PREFIX}/share/bash-completion/completions/"
	COMPONENT "bash"
)
install(FILES "${CMAKE_SOURCE_DIR}/misc/ninja-mode.el"
	DESTINATION "${CMAKE_INSTALL_PREFIX}/share/emacs/site-lisp/"
	COMPONENT "emacs"
)
install(FILES "${CMAKE_SOURCE_DIR}/misc/ninja.vim"
	DESTINATION "${CMAKE_INSTALL_PREFIX}/share/vim/addons/syntax/"
	COMPONENT "vim"
)
install(FILES "${CMAKE_SOURCE_DIR}/misc/zsh-completion"
	DESTINATION "${CMAKE_INSTALL_PREFIX}/share/zsh/vendor-completions/"
	COMPONENT "zsh"
)


install(TARGETS ninja_test
	RUNTIME
	COMPONENT "test"
)

set(CPACK_RPM_COMPONENT_INSTALL ON)
set(CPACK_DEB_COMPONENT_INSTALL ON)

set("CPACK_DEBIAN_PACKAGE_NAME" "${CPACK_PACKAGE_NAME}")
set("DEBIAN_PACKAGE_DEPENDS" "libc6 (>= 2.15), libstdc++6 (>= 5.2)")

set("CPACK_DEBIAN_VIM_PACKAGE_NAME" "vim-ninja-build")
set("CPACK_DEBIAN_EMACS_PACKAGE_NAME" "ninja-build-emacs")
set("CPACK_DEBIAN_ZSH_PACKAGE_NAME" "zsh-ninja-build")
set("CPACK_DEBIAN_BASH_PACKAGE_NAME" "ninja-build-bash-completion")

set("CPACK_DEBIAN_BIN_PACKAGE_NAME" "ninja-build")
set("CPACK_COMPONENT_BIN_DISPLAY_NAME" "ninja binary")
set("CPACK_DEBIAN_BIN_PACKAGE_ENHANCES" "cmake, kati")
set("CPACK_DEBIAN_BIN_PACKAGE_DEPENDS" "${DEBIAN_PACKAGE_DEPENDS}${DEPENDS_ON_LIBNINJA}")
set("CPACK_DEBIAN_BIN_PACKAGE_RECOMMENDS" "${CPACK_DEBIAN_VIM_PACKAGE_NAME}, ${CPACK_DEBIAN_EMACS_PACKAGE_NAME}, ${CPACK_DEBIAN_ZSH_PACKAGE_NAME}, ${CPACK_DEBIAN_BASH_PACKAGE_NAME}")

set("CPACK_DEBIAN_TEST_PACKAGE_NAME" "ninja-build-test")
set("CPACK_COMPONENT_TEST_DISPLAY_NAME" "A test for ninja")
set("CPACK_COMPONENT_TEST_DISABLED" TRUE)
set("CPACK_DEBIAN_TEST_PACKAGE_DEPENDS" "${DEBIAN_PACKAGE_DEPENDS}${DEPENDS_ON_LIBNINJA}")

get_cmake_property(CPACK_COMPONENTS_ALL COMPONENTS)
list(REMOVE_ITEM CPACK_COMPONENTS_ALL "test" "Unspecified")

include(CPack)
